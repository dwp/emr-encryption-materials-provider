plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}


group = 'uk.gov.dwp.dataworks.encryptionmaterialsprovider'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    shadow 'com.amazonaws:aws-java-sdk:1.11.567'
    shadow 'org.apache.hadoop:hadoop-common:2.4.0'
    implementation 'com.google.guava:guava:28.1-jre'
    implementation 'commons-io:commons-io:2.4'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation("com.amazonaws:aws-java-sdk:1.11.567")
    //S3 Mock depends on different implementations of the excluded modules
    testImplementation("org.apache.hadoop:hadoop-common:2.4.0"){
        exclude(group : 'org.slf4j', module: 'slf4j-log4j12' )
        exclude(group : 'javax.servlet', module: 'servlet-api')
        exclude(group : 'org.mortbay.jetty', module: 'servlet-api')
    }
    testImplementation("com.fasterxml.jackson.module:jackson-module-kotlin:2.10.0")
    testImplementation("io.kotlintest:kotlintest-runner-junit5:3.3.2")
    testImplementation("org.assertj:assertj-core:3.12.2")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.3.1")
    testImplementation("com.adobe.testing:s3mock-junit5:2.1.16")
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.3.1')
    testImplementation("org.mockito:mockito-core:2.21.0")
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
}

mainClassName = "uk.gov.dwp.dataworks.encryptionmaterialsprovider.DWEncryptionMaterialsProvider"

shadowJar {
    zip64(true)
    manifest {
        attributes(
                'Class-Path': sourceSets.main.runtimeClasspath.files.collect{it.name}.join(' '),
                'Main-Class': 'uk.gov.dwp.dataworks.encryptionmaterialsprovider.DWEncryptionMaterialsProvider')
    }
}


